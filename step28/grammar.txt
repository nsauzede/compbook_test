program           = (typedef | function-definition | global-variable)*
function          = declspec declarator
type-suffix       = "(" func-params
    | "[" num "]" type-suffix
    | epsilon
func-params       = (param ("," param)*)? ")"
param             = declspec declarator
type-name         = declspec abstract-declarator
abstract-declarator = "*"* ("(" abstract-declarator ")")? type-suffix
declaration       = declspec (declarator ("=" expr)? ("," declarator ("=" expr)?)*)? ";"
declarator        = "*"* ("(" ident ")" | "(" declarator ")" | ident) type-suffix
declspec          = ("void" | "char" | "short" | "int" | "long"
    | "typedef"
    | struct-decl | union-decl | typedef-name)+
struct-decl       = struct-union-decl
union-decl        = struct-union-decl
struct-union-decl = ident? ("{" struct-members)?
struct-members    = (declspec declarator ("," declarator)* ";")*
stmt              = "return" expr ";"
    | "if" "(" expr ")" stmt ("else" stmt)?
    | "for" "(" expr-stmt expr? ";" expr? ")" stmt
    | "while" "(" expr ")" stmt
    | "{" compound-stmt
    | expr-stmt
compound-stmt     = (typedef | declaration | stmt)* "}"
expr-stmt         = expr? ";"
expr              = assign ("," expr)?
assign            = equality ("=" assign)?
equality          = relational ("==" relational | "!=" relational)*
relational        = add ("<" add | "<=" add | ">" add | ">=" add)*
add               = mul ("+" mul | "-" mul)*
mul               = unary ("*" unary | "/" unary)*
cast              = "(" type-name ")" cast | unary
unary             = ("+" | "-" | "*" | "&") cast
    | postfix
postfix           = primary ("[" expr "]" | "." ident | "->" ident)*
funcall           = ident "(" (assign ("," assign)*)? ")"
primary           = "(" "{" stmt+ "}" ")"
    | "(" expr ")"
    | "sizeof" "(" type-name ")"
    | "sizeof" unary
    | ident func-args?
    | num
    | '"' str '"'
